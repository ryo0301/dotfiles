if status is-interactive
  # Kiro CLI pre script
  if test -x ~/.local/bin/kiro-cli
    eval (~/.local/bin/kiro-cli init fish pre)
  end

  # global export
  set -gx EDITOR vim
  set -gx PAGER less


  # alias
  ## eza
  function ls
    eza -F $argv
  end

  function ll
    ls -l $argv
  end

  function l.
    ls -la $argv
  end

  function lt
    ls -T $argv
  end

  ## tree
  function tree
    command tree -aCF --dirsfirst --noreport $argv
  end


  # fish bind
  function __go_version_search
    set -l system_prefix (brew --prefix)
    set -l gover (command ls "$HOME/sdk/" 2> /dev/null | cat (echo system "($system_prefix/bin/go)" | psub) - | fzf --no-multi)
    if test -n "$gover"
      if ! test (string match -r "[0-9.]+" "$gover")
        set -f path "$system_prefix/bin/go"
      else if ! test -x "$HOME/sdk/$gover/bin/go"
        set -f path "$system_prefix/bin/go"
      else
        set -f path "$HOME/sdk/$gover/bin/go"
      end
      ln -sf $path "$HOME/bin/go"
      go version
    end
    commandline -f repaint
  end

  function __ghq_repository_search
    set -l opts '--preview=[ -r {}/README.md ]; and glow {}/README.md'
    ghq list --full-path | fzf $opts | read select
    [ -n "$select" ]; and cd "$select"
    commandline -f repaint
  end

  function __agent_search
    ~/.local/bin/kiro-cli agent list 2>&1 \
      | sed 's/\x1b\[[0-9;]*m//g' \
      | grep '^[* ] ' \
      | sed 's/^..//' \
      | awk '{name=$1; scope=$2; desc=""; for(i=3;i<=NF;i++) desc=desc (i>3?" ":"") $i; if(scope=="(Built-in)") name="\033[38;5;244m"name"\033[0m"; print name"\t"scope"\t"desc}' \
      | fzf --ansi --delimiter='\t' --with-nth=1 --preview='printf "\033[1mScope\033[0m\n%s\n\n\033[1mDescription\033[0m\n%s\n" {2} {3}' \
      | sed 's/\x1b\[[0-9;]*m//g' \
      | cut -f1 \
      | read -l agent
    if test -n "$agent"
      commandline "kiro-cli chat --agent $agent"
      commandline -f repaint
      commandline -f execute
    end
    commandline -f repaint
  end

  function __ctrl_g_menu
    set -l choice (printf 'agent\nrepo\ngo' | fzf --preview='bash -c "case {} in agent) echo Kiroエージェントを選択して起動;; repo) echo ghqリポジトリに移動;; go) echo Goバージョンを切り替え;; esac"')
    switch $choice
      case agent
        __agent_search
      case repo
        __ghq_repository_search
      case go
        __go_version_search
      case '*'
        commandline -f repaint
    end
  end

  function fish_user_key_bindings
    bind \cg '__ctrl_g_menu'
  end


  # less
  set -gx LESS "-iMR"
  set -gx LESS_TERMCAP_mb \e'[01;35m'      # Begins blinking.
  set -gx LESS_TERMCAP_md \e'[01;35m'      # Begins bold.
  set -gx LESS_TERMCAP_me \e'[0m'          # Ends mode.
  set -gx LESS_TERMCAP_se \e'[0m'          # Ends standout-mode.
  set -gx LESS_TERMCAP_so \e'[00;47;30m'   # Begins standout-mode.
  set -gx LESS_TERMCAP_ue \e'[0m'          # Ends underline.
  set -gx LESS_TERMCAP_us \e'[01;34m'      # Begins underline.

  # git
  set __fish_git_prompt_showdirtystate 'yes'
  set __fish_git_prompt_showstashstate 'yes'
  set __fish_git_prompt_showuntrackedfiles 'yes'
  set __fish_git_prompt_showupstream 'auto'
  set __fish_git_prompt_color_branch yellow
  set __fish_git_prompt_color_upstream_ahead green
  set __fish_git_prompt_color_upstream_behind red

  #set __fish_git_prompt_char_dirtystate '*'
  #set __fish_git_prompt_char_stagedstate '+'
  #set __fish_git_prompt_char_invalidstate '#'
  #set __fish_git_prompt_char_stashstate '$'
  #set __fish_git_prompt_char_untrackedfiles '%'
  #set __fish_git_prompt_char_upstream_equal '='
  #set __fish_git_prompt_char_upstream_behind '<'
  #set __fish_git_prompt_char_upstream_ahead '>'
  #set __fish_git_prompt_char_upstream_diverged '<>'

  # fzf
  set -gx FZF_DEFAULT_OPTS '--height 50% --border sharp --reverse --cycle --no-mouse --color=border:blue,fg:252,bg:237,hl:4,fg+:231,bg+:24,hl+:1'


  # fish
  set fish_greeting

  # fish-bd
  set -gx BD_OPT "insensitive"

  # homebrew
  if test -x "/opt/homebrew/bin/brew"
    eval (/opt/homebrew/bin/brew shellenv)
  end
  set -gx HOMEBREW_GITHUB_API_TOKEN (security find-generic-password -a $USER -s ghp.homebrew -w)

  # Go
  fish_add_path (go env GOPATH)/bin

  # Java
  set -gx JAVA_HOME (/usr/libexec/java_home -v 17)

  # Rust
  set -gx RUST_BACKTRACE full
  if test -r "$HOME/.cargo/env"
    source "$HOME/.cargo/env"
  end

  # direnv
  direnv hook fish | source

  #volta
  set -gx VOLTA_HOME "$HOME/.volta"
  fish_add_path $VOLTA_HOME/bin

  # rbenv
  rbenv init - | source

  # delta
  set -gx DELTA_PAGER cat

  # AWS
  complete -c aws -f -a '(begin; set -lx COMP_SHELL fish; set -lx COMP_LINE (commandline); aws_completer; end)'

  # OpenAI
  set -gx OPENAI_API_KEY (security find-generic-password -a $USER -s openai.apikey -w)

  # user script
  fish_add_path -gP $HOME/bin

  # Kiro CLI
  # Temporarily discontinuing use since it doesn’t work properly with the Pro license.
  #set -gx AMAZON_Q_SIGV4 "1"

  # Kiro CLI post script
  if test -x ~/.local/bin/kiro-cli
    eval (~/.local/bin/kiro-cli init fish post)
  end
end
